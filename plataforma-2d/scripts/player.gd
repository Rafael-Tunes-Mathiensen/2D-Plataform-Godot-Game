extends CharacterBody2D

enum PlayerState {
	idle,
	walk,
	jump,
	fall,
	duck,
	slide,
	dead
}

@onready var anim: AnimatedSprite2D = $AnimatedSprite2D
@onready var collision_shape: CollisionShape2D = $CollisionShape2D
@onready var reload_timer: Timer = $ReloadTimer

@export var max_speed = 150
@export var acceleration = 475
@export var deceleration = 575
@export var slide_deceleration = 100
@export var JUMP_VELOCITY = -300.0
@export var max_jump_count = 2

var jump_count = 0
var direction = 0
var status: PlayerState

func _ready() -> void:
	go_to_idle_state()

func _physics_process(delta: float) -> void:
	if not is_on_floor():
		velocity += get_gravity() * delta
	
	match status:
		PlayerState.idle:
			idle_state(delta)
		PlayerState.walk:
			walk_state(delta)
		PlayerState.jump:
			jump_state(delta)
		PlayerState.fall:
			fall_state(delta)
		PlayerState.duck:
			duck_state(delta)
		PlayerState.slide:
			slide_state(delta)
		PlayerState.dead:
			dead_state(delta)
	
	move_and_slide()

func go_to_idle_state():
	status = PlayerState.idle
	anim.play("Idle")

func go_to_walk_state():
	status = PlayerState.walk
	anim.play("Walk")

func go_to_jump_state():
	status = PlayerState.jump
	anim.play("Jump")
	velocity.y = JUMP_VELOCITY
	jump_count += 1

func go_to_fall_state():
	status = PlayerState.fall
	anim.play("Fall")

func go_to_duck_state():
	status = PlayerState.duck
	anim.play("Duck")
	set_small_collider()

func exit_from_duck_state():
	set_large_collider()
	
func go_to_slide_state():
	status = PlayerState.slide
	anim.play("Slide")
	set_small_collider()

func exit_from_slide_state():
	set_large_collider()
	
func go_to_dead_state():
	status = PlayerState.dead
	anim.play("Dead")
	velocity = Vector2.ZERO
	reload_timer.start()
	
func idle_state(delta):
	move(delta)
	if velocity.x != 0:
		go_to_walk_state()
		return
		
	if Input.is_action_just_pressed("jump") and is_on_floor():
		go_to_jump_state()
		return
	if Input.is_action_pressed("duck"):
		go_to_duck_state()
		return

func walk_state(delta):
	move(delta)
	if velocity.x == 0:
		go_to_idle_state()
		return
		
	if Input.is_action_just_pressed("jump") and is_on_floor():
		go_to_jump_state()
		return
		
	if Input.is_action_just_pressed("duck"):
		go_to_slide_state()
		return
		
	if !is_on_floor():
		jump_count += 1
		go_to_fall_state()
		return

func jump_state(delta):
	move(delta)
	if Input.is_action_just_pressed("jump") and can_jump():
		go_to_jump_state()
	
	if velocity.y > 0:
		go_to_fall_state()
		return

func slide_state(delta):
	velocity.x = move_toward(velocity.x, 0, slide_deceleration * delta)
	
	if Input.is_action_just_released("duck"):
		exit_from_slide_state()
		go_to_walk_state()
		return

	if velocity.x == 0:
		exit_from_slide_state()
		go_to_duck_state()
		return

func dead_state(_delta):
	pass

func fall_state(delta):
	move(delta)
	if Input.is_action_just_pressed("jump") and can_jump():
		go_to_jump_state()
	
	if is_on_floor():
		jump_count = 0
		if velocity.x == 0:
			go_to_idle_state()
		else:
			go_to_walk_state()
		return

func duck_state(_delta):
	update_direction()
	if Input.is_action_just_released("duck"):
		exit_from_duck_state()
		go_to_idle_state()
		return

func move(delta):
	update_direction()
	
	if direction:
		velocity.x = move_toward(velocity.x, direction * max_speed, acceleration * delta)
	else:
		velocity.x = move_toward(velocity.x, 0, deceleration * delta)
	
func update_direction():
	direction = Input.get_axis("left", "right")
	
	if direction < 0:
		anim.flip_h = true
	elif direction > 0:
		anim.flip_h = false

func can_jump() -> bool:
	return jump_count < max_jump_count	
	
func set_small_collider():
	collision_shape.shape.radius = 6
	collision_shape.shape.height = 12
	collision_shape.position.y = 9

func set_large_collider():
	collision_shape.shape.radius = 7
	collision_shape.shape.height = 20
	collision_shape.position.y = 5.5
	

func _on_hitbox_area_entered(area: Area2D) -> void:
	if velocity.y > 0:
		# inimigo morre
		area.get_parent().take_damage()
		go_to_jump_state()
	else:
		# player morre
		if status != PlayerState.dead:
			go_to_dead_state()


func _on_reload_timer_timeout() -> void:
	get_tree().reload_current_scene()
